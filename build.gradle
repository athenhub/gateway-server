plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.7'
	id 'io.spring.dependency-management' version '1.1.7'
    id 'checkstyle'
    id 'com.diffplug.spotless' version '6.25.0'
}

group = 'com.athenhub'
version = '0.0.1-SNAPSHOT'
description = 'gateway-server'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
    mavenCentral()
    maven {
        name = "GitHubPublicPackages"
        url = uri("https://maven.pkg.github.com/athenhub/common")
        credentials {
            // gradle.properties 나 환경변수에서 읽어옴
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}


ext {
	set('springCloudVersion', "2025.0.0")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.cloud:spring-cloud-starter-config'
	implementation 'org.springframework.cloud:spring-cloud-starter-gateway-server-webflux'
	implementation 'org.springframework.cloud:spring-cloud-starter-loadbalancer'
	implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

    // Security + OAuth 관련
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'

    implementation 'jakarta.servlet:jakarta.servlet-api:6.0.0'

    implementation 'com.athenhub:common-core:1.2.0'

    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

checkstyle {
    toolVersion = "12.1.2"
    configFile = rootProject.file("config/checkstyle/checkstyle.xml")
    showViolations = true
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = false
        html.required = true
    }
}

tasks.named("check") {
    dependsOn "checkstyleMain", "checkstyleTest"
}

spotless {
    java {
        googleJavaFormat() // Google Java 스타일 강제
        target "src/**/*.java"
    }
}

tasks.check {
    dependsOn "spotlessCheck"
}